Variable k:ℕ
Variable n:ℕ

Metavariable ρ:Frame
Metavariable σ(ℕ):Frame
Metavariable τ(ℕ):Frame

Metavariable R:Reference
Metavariable S(ℕ):Reference
Metavariable T(ℕ):Reference

Metavariable P(ℕ):Statement

MetaLemma (T(zero))
  P(zero)
  Proof
    P(zero) from R

MetaLemma (T(successor(k)))
  P(successor(k))
  Proof
    P(k) from T(k)
    P(k) ⇒ P(successor(k)) from S(k)
  Therefore
    P(successor(k)) by ModusPonens

Rule (Induction)
  Premises
    ρ |= T(zero) :: P(zero)
    σ(k) |= S(k) :: (P(k) ⇒ P(successor(k)))
  Conclusion
    ∃[τ(n)] τ(n) |= T(n) :: P(n)
  Proof
    n = zero ∨ n = successor(k) by ZeroOrSuccessor
    Suppose
      n = zero
    Hence
      ρ |= T(zero) :: P(zero)
      [τ(zero)] is free in τ(zero) |= T(zero) :: P(zero) by FreeMetavariable
      ∃[τ(zero)] τ(zero) |= T(zero) :: P(zero) by MetaExistentialIntroduction
    Then
      ∃[τ(n)] τ(n) |= T(n) :: P(n)
    n = zero ⇒ ∃[τ(n)] τ(n) |= T(n) :: P(n) by ConditionalProof
    Suppose
      n = successor(k)
    Hence
      σ(k), T(k) :: P(k) |= T(successor(k)) :: P(successor(k))
      [τ(successor(k))] is free in τ(successor(k)) |= T(successor(k)) :: P(successor(k)) by FreeMetavariable
      ∃[τ(successor(k))] τ(successor(k)) |= T(successor(k)) :: P(successor(k)) by MetaExistentialIntroduction
    Then
      ∃[τ(n)] τ(n) |= T(n) :: P(n)
    n = successor(k) ⇒ ∃[τ(n)] τ(n) |= T(n) :: P(n) by ConditionalProof
  Therefore
    ∃[τ(n)] τ(n) |= T(n) :: P(n) by ProofByCases
